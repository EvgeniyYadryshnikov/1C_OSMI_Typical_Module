Функция СоздатьКартуНаСервисеОСМИ() Экспорт
	
	Карта1С = ОСМИ_ОбщиеСервер.Получить1СКарту(Ссылка);
	Если Карта1С = Неопределено Тогда  
		ОСМИ_ОбщиеСервер.ЗарегистрироватьОшибкуОСМИ("Нет объекта 1с, связанного с электронной картой: " + Код, Ссылка);
		Возврат Новый Структура("Успех", Ложь);
	КонецЕсли;	

	СтруктураПараметров = Справочники.ОСМИ_ЭлектронныеКарты.СформироватьСтруктуруДанныхКарты(Шаблон, Карта1С, ЭтотОбъект, Истина);
	  
	Возврат ОСМИ_РаботаСAPI.СоздатьКарту(Код, Шаблон.Наименование, Истина, СтруктураПараметров.СтруктураДанныхКарты);
	
КонецФункции

Функция УстановитьЗначенияПолей(ОтправлятьПушПринудительно = Ложь, ОбновитьКарту = Ложь, Карта1С = Неопределено) Экспорт
	
	Если ЭтоНовый() Тогда
		Сообщить("Перед началом установки полей электронной карты ее необходимо записать!");
		Возврат Ложь;
	КонецЕсли;
	
	Если Карта1С = Неопределено Тогда  
		Карта1С = ОСМИ_ОбщиеСервер.Получить1СКарту(Ссылка);
		Если Карта1С = Неопределено Тогда  
			ОСМИ_ОбщиеСервер.ЗарегистрироватьОшибкуОСМИ("Нет объекта 1с, связанного с электронной картой: " + Код, Ссылка);
			Возврат Новый Структура("Успех", Ложь);
		КонецЕсли;	
	КонецЕсли;	
	
	ДанныеКарты = ОСМИ_РаботаСAPI.ЗапроситьИнформациюПокарте(Код);
	Если ДанныеКарты.Успех = Ложь Тогда
		Если НЕ ЗначениеЗаполнено(Справочники.ОСМИ_ЭлектронныеКарты.СоздатьКартуИзКарты1С(Карта1С, Шаблон, ЭтотОбъект)) Тогда
			ОСМИ_ОбщиеСервер.ЗарегистрироватьОшибкуОСМИ("Не удачная попытка обновления полей электронной карты: " + Код, Ссылка);
			Возврат Ложь;
		Иначе
			Возврат Истина; //При создании карты все поля установятся автоматически
		КонецЕсли;
	КонецЕсли;
	
	СтруктураПараметров = Справочники.ОСМИ_ЭлектронныеКарты.СформироватьСтруктуруДанныхКарты(Шаблон, Карта1С, ЭтотОбъект, ОбновитьКарту);
	  
	Результат = ОСМИ_РаботаСAPI.ОбновитьЗначенияКарты(Код, СтруктураПараметров.СтруктураДанныхКарты,
		ОтправлятьПушПринудительно ИЛИ СтруктураПараметров.ОтправлятьПуш);
		
	Если НЕ Результат.Успех Тогда
		ОСМИ_ОбщиеСервер.ЗарегистрироватьОшибкуОСМИ("Не удачная попытка обновления полей электронной карты: " + Код, Ссылка);
		Возврат Ложь;
	КонецЕсли;

	ОбновитьЗначенияССервера(Результат);
		
	Возврат Истина;
	
КонецФункции

Функция ОбновитьЗначенияССервера(ДанныеКарты = Неопределено) Экспорт
	
	Если ДанныеКарты = Неопределено Тогда
		ДанныеКарты = ОСМИ_РаботаСAPI.ЗапроситьИнформациюПоКарте(Код);
		Если НЕ ДанныеКарты.Успех Тогда
			ОСМИ_ОбщиеСервер.ЗарегистрироватьОшибкуОСМИ("Ошибка получения данных о электронной карте с сервиса ОСМИ. Карта: " + Код, Ссылка);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ПоляКарты.Очистить();
	Для Каждого Значение  из ДанныеКарты.Ответ.values Цикл
		ПолеКарты = ПоляКарты.Добавить();
		ПолеКарты.Поле = Значение.label;
		ПолеКарты.Значение = Значение.value; 
	КонецЦикла;
		
	ДополнительнаяИнформация = "";
	Для Каждого СтрокаДИ из ДанныеКарты.Ответ.general Цикл
		ДополнительнаяИнформация = ДополнительнаяИнформация + 
			?(ЗначениеЗаполнено(ДополнительнаяИнформация), Символы.ПС, "") +
			СтрокаДИ.Ключ + " : " + СтрокаДИ.Значение;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции
